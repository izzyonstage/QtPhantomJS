@ECHO OFF

SET ERRORLEVEL=0
SET "INITIAL_DIR=%CD%"
SET "WD=%~dp0"
SET "PATH=%WD%;%PATH%"
PUSHD %WD%
    CALL :LAUNCH_VISUAL_STUDIO
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :CONFIGURE_PATHS
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    IF EXIST output RMDIR /S /Q "%WD%output"
    MKDIR output
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_SQLITE
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_ZLIB
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_LIBXML
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_OPENSSL
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_ICU
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_QTBASE
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_QWEBKIT
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

    CALL :BUILD_PHANTOMJS
    IF %ERRORLEVEL% NEQ 0 GOTO :ERROR

	CALL :PACKAGE_OUTPUT
	IF %ERRORLEVEL% NEQ 0 GOTO :ERROR
POPD
ECHO Build process completed
GOTO:EOF
:ERROR:
ECHO Failed with error #%ERRORLEVEL%.
cd %INITIAL_DIR%
EXIT /B 1



:LAUNCH_VISUAL_STUDIO:
ECHO Launching VS2015 Environment...
SET "PROG32_ROOT=%programfiles%"
IF "%programfiles(x86)%" NEQ "" SET "PROG32_ROOT=%programfiles(x86)%"
SET "VS2015=%PROG32_ROOT%\Microsoft Visual Studio 14.0\VC\vcvarsall.bat"
IF [%VisualStudioVersion%] NEQ [14.0] (
    "%VS2015%" x86
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
)
GOTO:EOF



:CONFIGURE_PATHS:
REM default configured paths
SET "CYGWIN_INSTALL_DIR=C:\Cygwin64\bin"
SET "TCL_INSTALL_DIR=C:\ActiveTcl"
REM only edit above this line
ECHO Mapping Tcl...
SET "PATH=%PATH%;%TCL_INSTALL_DIR%\bin"
SET "LIB=%TCL_INSTALL_DIR%\lib;%LIB%"
SET "INCLUDE=%TCL_INSTALL_DIR%\include;%INCLUDE%"
ECHO Mapping Jom...
SET "PATH=%WD%tools\jom;%PATH%"
GOTO:EOF



:BUILD_SQLITE:
ECHO Building Sqlite...
SET "SQLITE_DIR=%WD%output\sqlite"
IF NOT EXIST %SQLITE_DIR% MKDIR %SQLITE_DIR%
PUSHD output\sqlite
    CALL nmake /f "%WD%sqlite\Makefile.msc" libsqlite3.lib TOP="%WD%sqlite" NO_TCL=1 USE_CRT_DLL=1 LIBTCL=tcl86t.lib
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
POPD
SET "PATH=%SQLITE_DIR%;%PATH%"
SET "LIB=%SQLITE_DIR%;%LIB%"
SET "INCLUDE=%SQLITE_DIR%;%INCLUDE%"
GOTO:EOF



:BUILD_ZLIB:
ECHO Building ZLib...
SET "ZLIB_BUILD_DIR=%WD%output\zlib"
SET "ZLIB_INCS=%ZLIB_BUILD_DIR%\include"
SET "ZLIB_LIBS=%ZLIB_BUILD_DIR%\lib"
SET "ZLIB_BINS=%ZLIB_BUILD_DIR%\bin"
PUSHD zlib
    CALL nmake /f win32/Makefile.msc clean
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL nmake /f win32/Makefile.msc
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    REM copies zlib.lib & zdll.lib
    CALL xcopy "%WD%zlib\*.lib" "%ZLIB_LIBS%" /I /Q /Y
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    REM Copies zlib1.dll
    CALL xcopy "%WD%zlib\*.dll" "%ZLIB_BINS%" /I /Q /Y
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    REM Copies root header files
    CALL xcopy "%WD%zlib\*.h" "%ZLIB_INCS%" /I /Q /Y
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
POPD
SET "PATH=%ZLIB_BINS%;%PATH%"
SET "LIB=%ZLIB_LIBS%;%LIB%"
SET "INCLUDE=%ZLIB_INCS%;%INCLUDE%"
GOTO:EOF



:BUILD_LIBXML:
ECHO Building Libxml2...
SET "LIBXML_DIR=%WD%output\libxml2"
PUSHD libxml2\win32
    CALL nmake /f Makefile.msvc clean
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL cscript configure.js compiler=msvc prefix="%LIBXML_DIR%" iconv=no zlib=yes xml_debug=no static=yes
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL nmake /f Makefile.msvc install
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
POPD
SET "PATH=%LIBXML_DIR%\bin;%PATH%"
SET "LIB=%LIBXML_DIR%\lib;%LIB%"
SET "INCLUDE=%LIBXML_DIR%\include\libxml2;%INCLUDE%"
GOTO:EOF



:BUILD_OPENSSL:
ECHO Building OpenSSL...
SET "OPENSSL_DIR=%WD%output\openssl"
SET "PATH=%PATH%;%WD%nasm"
PUSHD openssl
    IF EXIST ms\ntdll.mak (
        CALL nmake -f ms\nt.mak clean
        IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
    )

    CALL perl Configure VC-WIN32 no-asm no-shared --prefix="%OPENSSL_DIR%" --openssldir="%OPENSSL_DIR%\ssl"
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL ms\do_ms.bat
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL nmake -f ms\nt.mak
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL nmake -f ms\nt.mak test
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL nmake -f ms\nt.mak install
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%
POPD
SET "PATH=%OPENSSL_DIR%\bin;%PATH%"
SET "LIB=%OPENSSL_DIR%\lib;%LIB%"
SET "INCLUDE=%OPENSSL_DIR%\include;%INCLUDE%"
GOTO:EOF



:BUILD_ICU:
ECHO Building ICU...
SET "ICU_DIR=%WD%output\icu"
PUSHD icu\source
    SET "PATH=%PATH%;%CYGWIN_INSTALL_DIR%"

    FOR /F "delims=" %%a IN ('cygpath -p -u "%ICU_DIR%"') DO @SET "INSTALL_DIR_CYGWIN=%%a"
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL dos2unix *
    CALL dos2unix -f configure
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL bash runConfigureICU Cygwin/MSVC -prefix="%INSTALL_DIR_CYGWIN%" --enable-static --disable-shared
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL make
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL make check
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL make install
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL SET "PATH=%%PATH:;%CYGWIN_INSTALL_DIR%=%%"
POPD
SET "PATH=%ICU_DIR%\bin;%PATH%"
SET "LIB=%ICU_DIR%\lib;%LIB%"
SET "INCLUDE=%ICU_DIR%\include;%INCLUDE%"
GOTO:EOF



:BUILD_QTBASE:
ECHO Building Qt5...
SET "QT_DIR=%WD%output\qt\5.7.1"
PUSHD qt
    SET "PATH=%WD%qt\gnuwin32\bin;%PATH%"

    CALL configure -mp -static -release -strip -ltcg^
 -qt-zlib -qt-pcre -qt-libpng -qt-libjpeg -qt-freetype^
 -qt-sql-sqlite -qt-sql-odbc -icu -opengl desktop -largefile^
 -no-qml-debug -no-dbus -no-audio-backend -no-angle^
 -opensource -confirm-license -make libs -skip qtwebengine^
 -skip qtconnectivity -skip qtserialport -skip qtlocation^
 -skip qtsensors -skip qtgamepad -skip qtdoc -skip qtpurchasing^
 -nomake tools -nomake examples -nomake tests^
 -openssl-linked OPENSSL_LIBS="-lws2_32 -lgdi32 -ladvapi32 -lcrypt32 -luser32 -llibeay32 -lssleay32"^
 -prefix "%QT_DIR%" -platform win32-msvc2015
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL jom install
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    IF NOT EXIST "%QT_DIR%\lib\Qt5Widgets.lib" (
        ECHO QtBase Build Failed - Missing Qt5Widgets.lib
        EXIT /B 1
    )
POPD
SET "PATH=%QT_DIR%\bin;%PATH%"
SET "LIB=%QT_DIR%\lib;%LIB%"
SET "INCLUDE=%WD%qt\qtbase\src\3rdparty\libjpeg;%INCLUDE%"
SET "INCLUDE=%WD%qt\qtbase\src\3rdparty\libpng;%INCLUDE%"
SET "INCLUDE=%QT_DIR%\include;%INCLUDE%"
GOTO:EOF



:BUILD_QWEBKIT:
ECHO Building QtWebKit...
PUSHD qtwebkit
    CALL cmake^
 -DPORT="Qt" -DCMAKE_BUILD_TYPE=Release -Wno-dev -DDEVELOPER_MODE=OFF --no-warn-unused-cli^
 -DENABLE_3D_TRANSFORMS=ON -DENABLE_ACCELERATED_2D_CANVAS=ON -DENABLE_ALLINONE_BUILD=ON^
 -DENABLE_ES6_ARROWFUNCTION_SYNTAX=ON -DENABLE_ATTACHMENT_ELEMENT=OFF -DENABLE_BATTERY_STATUS=OFF^
 -DENABLE_CANVAS_PATH=ON -DENABLE_CANVAS_PROXY=OFF -DENABLE_CHANNEL_MESSAGING=ON^
 -DENABLE_ES6_CLASS_SYNTAX=ON -DENABLE_ES6_GENERATORS=ON -DENABLE_ES6_MODULES=OFF^
 -DENABLE_ES6_TEMPLATE_LITERAL_SYNTAX=ON -DENABLE_CSP_NEXT=OFF -DENABLE_CSS_DEVICE_ADAPTATION=OFF^
 -DENABLE_CSS_SHAPES=ON -DENABLE_CSS_GRID_LAYOUT=ON -DENABLE_CSS3_TEXT=OFF^
 -DENABLE_CSS3_TEXT_LINE_BREAK=OFF -DENABLE_CSS_BOX_DECORATION_BREAK=ON -DENABLE_TOOLS=OFF^
 -DENABLE_CSS_IMAGE_ORIENTATION=OFF -DENABLE_CSS_IMAGE_RESOLUTION=OFF -DENABLE_CSS_IMAGE_SET=ON^
 -DENABLE_CSS_REGIONS=ON -DENABLE_CSS_COMPOSITING=OFF -DENABLE_CUSTOM_ELEMENTS=OFF^
 -DENABLE_CUSTOM_SCHEME_HANDLER=OFF -DENABLE_DATALIST_ELEMENT=ON -DENABLE_DATA_TRANSFER_ITEMS=OFF^
 -DENABLE_DETAILS_ELEMENT=ON -DENABLE_DEVICE_ORIENTATION=OFF -DENABLE_DOM4_EVENTS_CONSTRUCTOR=ON^
 -DENABLE_DOWNLOAD_ATTRIBUTE=ON -DENABLE_FETCH_API=ON -DENABLE_FONT_LOAD_EVENTS=OFF^
 -DENABLE_FTPDIR=OFF -DENABLE_FULLSCREEN_API=ON -DENABLE_GAMEPAD=OFF -DENABLE_GEOLOCATION=OFF^
 -DENABLE_HIGH_DPI_CANVAS=OFF -DENABLE_ICONDATABASE=ON -DENABLE_INDEXED_DATABASE=ON^
 -DENABLE_INPUT_SPEECH=OFF -DENABLE_INPUT_TYPE_COLOR=ON -DENABLE_INPUT_TYPE_DATE=OFF^
 -DENABLE_INPUT_TYPE_DATETIME_INCOMPLETE=OFF -DENABLE_INPUT_TYPE_DATETIMELOCAL=OFF^
 -DENABLE_INPUT_TYPE_MONTH=OFF -DENABLE_INPUT_TYPE_TIME=OFF -DENABLE_INPUT_TYPE_WEEK=OFF^
 -DENABLE_INTL=ON -DENABLE_LEGACY_NOTIFICATIONS=OFF -DENABLE_LEGACY_VENDOR_PREFIXES=ON^
 -DENABLE_LEGACY_WEB_AUDIO=OFF -DENABLE_LINK_PREFETCH=ON -DENABLE_JIT=ON -DENABLE_MATHML=ON^
 -DENABLE_MEDIA_CAPTURE=OFF -DENABLE_MEDIA_SOURCE=OFF -DENABLE_MEDIA_STATISTICS=OFF^
 -DENABLE_MEDIA_STREAM=OFF -DENABLE_METER_ELEMENT=ON -DENABLE_MHTML=ON -DENABLE_MOUSE_CURSOR_SCALE=OFF^
 -DENABLE_NAVIGATOR_CONTENT_UTILS=OFF -DENABLE_NAVIGATOR_HWCONCURRENCY=ON -DENABLE_NETSCAPE_PLUGIN_API=OFF^
 -DENABLE_NOSNIFF=OFF -DENABLE_NOTIFICATIONS=ON -DENABLE_ORIENTATION_EVENTS=OFF -DENABLE_TEST_SUPPORT=OFF^
 -DENABLE_PERFORMANCE_TIMELINE=OFF -DENABLE_PROMISES=ON -DENABLE_PROXIMITY_EVENTS=OFF^
 -DENABLE_QUOTA=OFF -DENABLE_RESOLUTION_MEDIA_QUERY=OFF -DENABLE_RESOURCE_TIMING=OFF^
 -DENABLE_REQUEST_ANIMATION_FRAME=ON -DENABLE_SAMPLING_PROFILER=ON -DENABLE_SECCOMP_FILTERS=OFF^
 -DENABLE_SCRIPTED_SPEECH=OFF -DENABLE_SHADOW_DOM=OFF -DENABLE_STREAMS_API=ON -DENABLE_SUBTLE_CRYPTO=OFF^
 -DENABLE_SVG_FONTS=ON -DUSE_SYSTEM_MALLOC=OFF -DENABLE_TEMPLATE_ELEMENT=ON -DENABLE_WEBKIT2=OFF^
 -DENABLE_THREADED_COMPOSITOR=OFF -DENABLE_TEXT_AUTOSIZING=OFF -DENABLE_TOUCH_EVENTS=OFF^
 -DENABLE_TOUCH_SLIDER=OFF -DENABLE_TOUCH_ICON_LOADING=OFF -DENABLE_USER_TIMING=OFF^
 -DENABLE_VIBRATION=OFF -DENABLE_VIDEO=OFF -DENABLE_VIDEO_TRACK=OFF -DENABLE_WEBGL=ON^
 -DENABLE_WEBASSEMBLY=OFF -DENABLE_WEB_ANIMATIONS=OFF -DENABLE_WEB_AUDIO=OFF -DENABLE_WEB_REPLAY=OFF^
 -DENABLE_WEB_SOCKETS=ON -DENABLE_WEB_TIMING=ON -DENABLE_XSLT=OFF -DENABLE_FTL_JIT=OFF^
 -DENABLE_PRINT_SUPPORT=OFF -DENABLE_OPENGL=OFF -DENABLE_API_TESTS=OFF -DUSE_MEDIA_FOUNDATION=OFF^
 -DQT_BUNDLED_JPEG=ON -DQT_BUNDLED_PNG=ON -DQT_BUNDLED_ZLIB=OFF^
 -DSQLITE_LIBRARIES="%SQLITE_DIR%\libsqlite3.lib" -DSQLITE_INCLUDE_DIR="%SQLITE_DIR%"^
 -DLIBXML2_LIBRARIES="%LIBXML_DIR%\lib\libxml2_a.lib" -DLIBXML2_INCLUDE_DIR="%LIBXML_DIR%\include"^
 -DCMAKE_PREFIX_PATH="%QT_DIR%" -G "NMake Makefiles JOM"
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    ECHO Compiling QtWebKit...
	CALL jom install
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    IF NOT EXIST "%QT_DIR%\lib\Qt5WebKitWidgets.lib" (
        ECHO QtWebKit Build Failed - Missing Qt5WebKitWidgets.lib
        EXIT /B 1
    )
POPD
GOTO:EOF



:BUILD_PHANTOMJS:
ECHO Building PhantomJS...
PUSHD phantomjs
    SET "WEB_INSPECTOR_RESOURCES_DIR=%WD%qtwebkit\DerivedSources\WebInspectorUI"

    CALL qmake
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    CALL jom
    IF %ERRORLEVEL% NEQ 0 EXIT /B %ERRORLEVEL%

    IF NOT EXIST bin\phantomjs.exe (
        ECHO PhantomJS Build Failed - Missing phantomjs.exe
        EXIT /B 1
    )
POPD
GOTO:EOF



:PACKAGE_OUTPUT:
ECHO Packaging Output...
CALL 7z a phantomjs.zip .\phantomjs\bin\*
GOTO:EOF
